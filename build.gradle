plugins {
    id 'idea'
    id 'nebula.release' version '15.3.0'
    id 'org.springframework.boot' version '2.4.0-M4' apply false
    id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
}

group = 'dev.tomek'

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 15
    tasks.withType(JavaCompile).each {
        it.options.compilerArgs.add('--enable-preview')
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencies {
        def lombokVersion = '1.18.14'
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        // for serializing records
        compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.12.0-rc1'
        compileOnly 'com.fasterxml.jackson.core:jackson-annotations:2.12.0-rc1'
    }

    if (!project.name.endsWith("common")) {
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'

        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-webflux'
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
            runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
            implementation project(':common')

            implementation 'org.apache.pulsar:pulsar-client:2.6.1'
            implementation 'io.nats:jnats:2.8.0'

            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testImplementation 'io.projectreactor:reactor-test'
        }

        test {
            useJUnitPlatform()
        }
    }
}
